

// 1
Xpath = { SOI ~ Expr ~ EOI }
// 6
Expr = { ExprSingle ~ ("," ~ ExprSingle)* }
// 7
ExprSingle = { PrimaryExpr | IfExpr }
// [15]    	IfExpr 	   ::=    	"if" "(" Expr ")" "then" ExprSingle "else" ExprSingle
// 15
IfExpr = { "if" ~ "(" ~ Expr ~ ")" ~ "then" ~ ExprSingle ~ "else" ~ ExprSingle }
// 56
PrimaryExpr = _{ Literal | ParenthesizedExpr }
// 57
Literal = { DoubleLiteral | DecimalLiteral | IntegerLiteral | StringLiteral }
// [61]    	ParenthesizedExpr 	   ::=    	"(" Expr? ")"
// 61
ParenthesizedExpr = { "(" ~ Expr? ~ ")" }
// 113
IntegerLiteral = @{ Digits }
// 114
DecimalLiteral = @{ ("." ~ Digits ) | (Digits ~ "." ~ ASCII_DIGIT*) }
// 115
DoubleLiteral = @{ (("." ~ Digits) | (Digits ~ ("." ~ ASCII_DIGIT*)?)) ~ ^"e" ~ ("+"|"-")? ~ Digits }
// 116
StringLiteral = @{ (Quot ~ (EscapeQuot | !Quot ~ ANY)* ~ Quot) | (Apos ~ (EscapeApos | !Apos ~ ANY)* ~ Apos) }
// 119
EscapeQuot = { "\"\"" }
Quot = { "\"" }
// 120
EscapeApos = { "''" }
Apos = { "'" }
// 125
Digits = @{ ASCII_DIGIT+ }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
// [121]    	Comment 	   ::=    	"(:" (CommentContents | Comment)* ":)" 	/* ws: explicit */
   // 				/* gn: comments */
   // [126]    	CommentContents 	   ::=    	(Char+ - (Char* ('(:' | ':)') Char*))
// 121
COMMENT = _{ "(:" ~ (CommentContents | COMMENT)* ~ ":)" }
// 126
CommentContents = { ( !("(:" | ":)") ~ ANY)+ }